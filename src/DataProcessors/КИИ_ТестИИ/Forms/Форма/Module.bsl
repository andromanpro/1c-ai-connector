#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗагрузитьНастройкиФормы();
	ВыполнятьВФоне = Истина;
	ОбновитьОтображениеОтветаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройкиФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЭкспертПриИзменении(Элемент)
	
	СохранитьНастройкиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВФонеПриИзменении(Элемент)
	
	СохранитьНастройкиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура РазмерКонтекстаПриИзменении(Элемент)
	
	Если РазмерКонтекста < 0 Тогда
		РазмерКонтекста = 0;
	КонецЕсли;
	
	СохранитьНастройкиФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отправить(Команда)
	
	Если ПустаяСтрока(Промпт) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Введите промпт'"));
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Эксперт) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не заполнен Эксперт ИИ'"));
		Возврат;
	КонецЕсли;
	
	Элементы.Отправить.Доступность = Ложь;
	
	ДобавитьВИсториюНаСервере("user", Промпт);
	ОбновитьОтображениеОтветаНаСервере();
	
	Если ВыполнятьВФоне Тогда
		ЗапуститьЗапросВФоне();
	Иначе
		ВыполнитьЗапросСинхронно();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	Промпт = "";
	ОчиститьИсториюНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыполнениеЗапроса

&НаКлиенте
Процедура ВыполнитьЗапросСинхронно()
	
	РезультатЗапроса = ВыполнитьЗапросНаСервере();
	
	Элементы.Отправить.Доступность = Истина;
	
	ПроцессИтогиЗапроса(РезультатЗапроса);
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьЗапросНаСервере()
	
	СохранитьНастройкиФормы();
	
	Модель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Эксперт, "Модель");
	СтруктураПромпта = СформироватьСтруктураПромпта(Эксперт, Промпт);
	
	Результат = КИИ_КоннекторИИ.ЗапросКМодели(Модель, СтруктураПромпта);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьЗапросВФоне()
	
	ФоновоеЗадание = ЗапуститьЗапросВФонеНаСервере();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Запрос к модели ИИ...'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьЗавершениеФоновогоЗадания", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьЗапросВФонеНаСервере()
	
	СохранитьНастройкиФормы();
	
	Модель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Эксперт, "Модель");
	СтруктураПромпта = СформироватьСтруктураПромпта(Эксперт, Промпт);
	
	Возврат КИИ_КоннекторИИ.ЗапросКМоделиВФоне(Модель, СтруктураПромпта);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьЗавершениеФоновогоЗадания(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Отправить.Доступность = Истина;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(, Результат.КраткоеПредставлениеОшибки);
		ДобавитьВИсториюНаСервере("error", Результат.КраткоеПредставлениеОшибки);
		ОбновитьОтображениеОтветаНаСервере();
		Возврат;
	КонецЕсли;
	
	РезультатЗапроса = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	ПроцессИтогиЗапроса(РезультатЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцессИтогиЗапроса(РезультатЗапроса)
	
	Если РезультатЗапроса.ЭтоОшибка Тогда
		ПоказатьПредупреждение(, РезультатЗапроса.ТекстОшибки);
		ДобавитьВИсториюНаСервере("error", РезультатЗапроса.ТекстОшибки);
		ОбновитьОтображениеОтветаНаСервере();
		Возврат;
	КонецЕсли;
	
	ДобавитьОтветВИсторию(РезультатЗапроса);
	Промпт = "";
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСИсторией

&НаСервере
Процедура ДобавитьВИсториюНаСервере(Знач Роль, Знач Текст, Знач Метаинформация = "")
	
	НоваяСтрока = ИсторияДиалога.Добавить();
	НоваяСтрока.Роль = Роль;
	НоваяСтрока.Текст = Текст;
	НоваяСтрока.Метаинформация = Метаинформация;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОтветВИсторию(РезультатЗапроса)
	
	Метаинформация = СформироватьМетаинформацию(РезультатЗапроса);
	
	ДобавитьВИсториюНаСервере("assistant", РезультатЗапроса.ТекстОтвета, Метаинформация);
	ОбновитьОтображениеОтветаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Функция СформироватьМетаинформацию(РезультатЗапроса)
	
	МодельОтвета = ?(ПустаяСтрока(РезультатЗапроса.МодельОтвета), 
		"N/A", 
		РезультатЗапроса.МодельОтвета);
	
	Метаинформация = СтрШаблон(
		"&#9889; Токены: %1 | &#9201; Время: %2с | &#9874; Модель: %3",
		РезультатЗапроса.ВсегоТокенов,
		Формат(РезультатЗапроса.ВремяОтветаСекунд, "ЧЦ=3; ЧДЦ=1"),
		МодельОтвета);
	
	Возврат Метаинформация;
	
КонецФункции

&НаСервере
Процедура ОчиститьИсториюНаСервере()
	
	ИсторияДиалога.Очистить();
	ОбновитьОтображениеОтветаНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОтображениеДиалога

&НаСервере
Процедура ОбновитьОтображениеОтветаНаСервере()
	
	Если ИсторияДиалога.Количество() = 0 Тогда
		ОтветHTML = СформироватьПустуюИсториюHTML();
		Возврат;
	КонецЕсли;
	
	Шаблон = Обработки.КИИ_ТестИИ.ПолучитьМакет("ШаблонДиалога").ПолучитьТекст();
	
	СообщенияHTML = "";
	Для Каждого Сообщение Из ИсторияДиалога Цикл
		СообщенияHTML = СообщенияHTML + СформироватьHTMLСообщения(Сообщение);
	КонецЦикла;
	
	HTML = СтрЗаменить(Шаблон, "[MESSAGES]", СообщенияHTML);
	
	ОтветHTML = HTML;
	
КонецПроцедуры

&НаСервере
Функция СформироватьПустуюИсториюHTML()
	
	HTML = "
	|<!DOCTYPE html>
	|<html>
	|<head>
	|    <meta charset='UTF-8'>
	|    <style>
	|        body {
	|            font-family: Arial;
	|            padding: 20px;
	|            text-align: center;
	|            color: #999;
	|        }
	|    </style>
	|</head>
	|<body>
	|    <p>&#9998; История диалога пуста</p>
	|    <p>Задайте вопрос, чтобы начать общение</p>
	|</body>
	|</html>";
	
	Возврат HTML;
	
КонецФункции

&НаСервере
Функция СформироватьHTMLСообщения(Сообщение)
	
	Класс = "";
	ИмяРоли = "";
	Эмодзи = ""; 
	
	Если Сообщение.Роль = "user" Тогда
		Класс = "user";
		ИмяРоли = нСтр("ru='Пользователь'");
		Эмодзи = "&#9998;"; 
	ИначеЕсли Сообщение.Роль = "assistant" Тогда
		Класс = "assistant";
		ИмяРоли = нСтр("ru='ИИ эксперт'");
		Эмодзи = "&#9874;"; 
	Иначе 
		Сообщение.Роль = "error"; 
		Класс = "error";
		ИмяРоли = нСтр("ru='Ошибка'");
		Эмодзи = "&#9888;"; 
	КонецЕсли;
	
	ТекстСообщения = ОбработатьТекстСообщения(Сообщение.Текст);
	
	ХТМЛ = СтрШаблон("
			|<div class='message %1'>
			|    <div class='role'>%2 %3</div>
			|    <div class='content'>%4</div>",
			Класс, Эмодзи, ИмяРоли, ТекстСообщения);
	
	Если Не ПустаяСтрока(Сообщение.Метаинформация) Тогда
		ХТМЛ = ХТМЛ + СтрШаблон("
		|    <div class='meta'>%1</div>", Сообщение.Метаинформация);
	КонецЕсли;
	
	ХТМЛ = ХТМЛ + "
			|</div>";
	
	Возврат ХТМЛ;
	
КонецФункции

&НаСервере
Функция ОбработатьТекстСообщения(Текст)
	
	ТипКонтента = ОпределитьТипКонтента(Текст);
	
	Если ТипКонтента = "HTML" Тогда
		Возврат СтрШаблон("<pre><code class='language-html'>%1</code></pre>", 
			КИИ_МаркдаунПарсерКлиентСервер.ЭкранироватьHTML(Текст));
		
	ИначеЕсли ТипКонтента = "JSON" Тогда
		Возврат СтрШаблон("<pre><code class='language-json'>%1</code></pre>", 
			КИИ_МаркдаунПарсерКлиентСервер.ЭкранироватьHTML(Текст));
		
	ИначеЕсли ТипКонтента = "Markdown" Тогда
		Возврат КИИ_МаркдаунПарсерКлиентСервер.ПреобразоватьВHTML(Текст);
	Иначе
		ТекстHTML = КИИ_МаркдаунПарсерКлиентСервер.ЭкранироватьHTML(Текст);
		ТекстHTML = СтрЗаменить(ТекстHTML, Символы.ПС, "<br>");
		Возврат ТекстHTML;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ОпределитьТипКонтента(Знач Текст)
	
	ТекстОбрезанный = СокрЛП(Текст);
	
	// Проверяем в порядке от более специфичных к менее специфичным
	Если ЭтоHTML(ТекстОбрезанный) Тогда
		Возврат "HTML";
	КонецЕсли;
	
	Если ЭтоJSON(ТекстОбрезанный) Тогда
		Возврат "JSON";
	КонецЕсли;
	
	Если ЭтоMarkdown(Текст) Тогда
		Возврат "Markdown";
	КонецЕсли;
	
	Возврат "PlainText";
	
КонецФункции

&НаСервере
Функция ЭтоHTML(Знач Текст)
	
	ЕстьОткрывающийТег = СтрНачинаетсяС(Текст, "<!DOCTYPE")
		Или СтрНачинаетсяС(Текст, "<html")
		Или СтрНачинаетсяС(Текст, "<div")
		Или СтрНачинаетсяС(Текст, "<table");
	
	ЕстьЗакрывающийТег = СтрНайти(Текст, "</") > 0;
	
	Возврат ЕстьОткрывающийТег И ЕстьЗакрывающийТег;
	
КонецФункции

&НаСервере
Функция ЭтоJSON(Знач Текст)
	
	ЭтоОбъект = СтрНачинаетсяС(Текст, "{") И СтрЗаканчиваетсяНа(Текст, "}");
	ЭтоМассив = СтрНачинаетсяС(Текст, "[") И СтрЗаканчиваетсяНа(Текст, "]");
	
	Возврат ЭтоОбъект Или ЭтоМассив;
	
КонецФункции

&НаСервере
Функция ЭтоMarkdown(Знач Текст)
	
	// Блоки кода
	Если СтрНайти(Текст, "```") > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Заголовки
	Если СтрНайти(Текст, "##") > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Форматирование
	Если СтрНайти(Текст, "**") > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Ссылки
	Если СтрНайти(Текст, "](") > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Списки в начале строки
	Если СтрНайти(Текст, "- ") = 1 Или СтрНайти(Текст, "* ") = 1 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область НастройкиФормы

&НаСервере
Функция ПолучитьКлючНастроек()
	
	Возврат Метаданные.Обработки.КИИ_ТестИИ.ПолноеИмя();
	
КонецФункции

&НаСервере
Процедура ЗагрузитьНастройкиФормы()
	
	КлючОбъекта = ПолучитьКлючНастроек();
	
	Настройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		КлючОбъекта,
		"НастройкиФормы");
	
	Если ТипЗнч(Настройки) <> Тип("Структура") Тогда
		РазмерКонтекста = 5;
		Возврат;
	КонецЕсли;
	
	Если Настройки.Свойство("ПоследнийЭксперт") 
		И ЗначениеЗаполнено(Настройки.ПоследнийЭксперт) Тогда
		Эксперт = Настройки.ПоследнийЭксперт;
	КонецЕсли;
	
	Если Настройки.Свойство("ВыполнятьВФоне") Тогда
		ВыполнятьВФоне = Настройки.ВыполнятьВФоне;
	КонецЕсли;
	
	Если Настройки.Свойство("РазмерКонтекста") Тогда
		РазмерКонтекста = Настройки.РазмерКонтекста;
	Иначе
		РазмерКонтекста = 5;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиФормы()
	
	КлючОбъекта = ПолучитьКлючНастроек();
	
	Настройки = Новый Структура;
	Настройки.Вставить("ПоследнийЭксперт", Эксперт);
	Настройки.Вставить("ВыполнятьВФоне", ВыполнятьВФоне);
	Настройки.Вставить("РазмерКонтекста", РазмерКонтекста);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		КлючОбъекта,
		"НастройкиФормы",
		Настройки);
	
КонецПроцедуры
#КонецОбласти

#Область Вспомогательные

&НаСервере
Функция СформироватьСтруктураПромпта(Знач Эксперт, Знач Промпт)
	
	Результат = Новый Структура;
	Результат.Вставить("СистемныйПромпт", "");
	Результат.Вставить("ПользовательскийПромпт", "");
	Результат.Вставить("ИсторияСообщений", Новый Массив);
	
	ПромптЭксперта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Эксперт, "ОписаниеЭксперта");
	Результат.СистемныйПромпт = ПромптЭксперта;
	Результат.ПользовательскийПромпт = Промпт;
	
	Если ИсторияДиалога.Количество() > 1 Тогда
		
		КоличествоДоступных = ИсторияДиалога.Количество() - 1;
		
		Если РазмерКонтекста = 0 Тогда
			НачальнаяПозиция = 0;
		Иначе
			НачальнаяПозиция = Макс(0, КоличествоДоступных - РазмерКонтекста);
		КонецЕсли;
		
		Для Индекс = НачальнаяПозиция По ИсторияДиалога.Количество() - 1 Цикл
			
			Сообщение = ИсторияДиалога[Индекс];
			
			Если Сообщение.Роль = "error" Тогда
				Продолжить;
			КонецЕсли;
			
			Если Индекс = ИсторияДиалога.Количество() - 1 И Сообщение.Роль = "user" Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаИстории = Новый Структура;
			СтрокаИстории.Вставить("Роль", Сообщение.Роль);
			СтрокаИстории.Вставить("Текст", Сообщение.Текст);
			
			Результат.ИсторияСообщений.Добавить(СтрокаИстории);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти
